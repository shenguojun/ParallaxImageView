plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdk 21
        targetSdk 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

ext {
    GITHUB_REPO_PATH = "file://${rootDir}/repo"
    PUBLISH_GROUP_ID = 'com.shengj.repo'
    PUBLISH_ARTIFACT_ID = 'parallax-image-view'
    PUBLISH_VERSION = '0.0.2'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from "${rootDir}/src/main/java"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier 'javadoc'
    from "${buildDir}/dokka/javadoc"
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url = project.GITHUB_REPO_PATH
            }
        }
        publications {
            release(MavenPublication) {
                groupId = project.PUBLISH_GROUP_ID
                artifactId = project.PUBLISH_ARTIFACT_ID
                version = project.PUBLISH_VERSION
                artifact javadocJar
                artifact sourcesJar
                artifact bundleReleaseAar
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}